
~ erase_data
WARNING: This will erase all saved data, including any achievements and audio settings.
Are you sure about this?
- Yes
	Erasing data...
	do erase_data()
	Done.
- No, don't do it!
	Aborted.
=> END!

#######################################

# OPENING
~ opening

*ring ring ring* [do! phone_ring()]
# do pick_up_phone()
J: “Heyyyy man. How’s it hangin’?”
MC: “Ahh you know. As well as cleaning up someone else’s mess can go.”
J: “Haha you’re funny man. Funny. Look, so basically, it’s been about like 4? 5? Minutes since, uh, the unfortunate incident occurred."
MC: "OK. And how'd it go?"
J: "Ahmmmm, well, I kinda didn’t actually succeed at the beginning, so there are like… 3 different weapons that you’ll probably have to get rid of. And I thiiiink he got a call out to the police? So I’d give you like… 5 minutes until they get there. Anyway, I gotta go, got another job to take care of, good luck maaan”
*click* [do! slam_down_phone()]
MC: Sigh. I hate this job.
do start_game()
=> END!


~ opening_game
...
MC: Seriously? What a mess... I don't get paid enough for this.
MC: Seems like the mafia's really gone downhill lately.
MC: I'll just have to hide as much of the evidence as I can and get out of here before the cops show up.
MC: Oh well. Time for me to get to work...
=> END!


#######################################
# ENDINGS

~ ending_normal
you didn't hide enough evidence
=> END!

~ ending_vampire
they found the victim's blood in your stomach
=> END!

~ ending_webcam
you got caught from webcam footage
=> END!

~ ending_body
bruh why are you holding a dead body
=> END!


~ ending_gun
you got caught with the murder weapon
=> END!

~ ending_shootout
you were holding a gun and the police opened fire
=> END!


~ ending_time
you ran out of time and the police showed up
=> END!

~ ending_window
you got caught by the window guy
=> END!

~ ending_sleep
you got caught sleeping on the job
=> END!

~ ending_clean_hands
you had suspiciously clean hands
=> END!

~ ending_good
you got away
=> END!



#######################################
# EVENTS

~ window_event
*knock knock*
???: Hey! Anybody in there?

% MC: Great galloping gooseberries! Somebody's at the window!
% MC: Great googly moogly! Somebody's at the window!
% MC: Mother of meatballs! Somebody's at the window!
% MC: Flaming fudge nuggets! Somebody's at the window!
% MC: Hopping jalapeño jellies! Somebody's at the window!
% MC: Son of a biscuit! Somebody's at the window!
% MC: Son of a nutcracker! Somebody's at the window!
% MC: Sweet pickled pancakes! Somebody's at the window!

(From now on, time will go by faster until you deal with this person.)
(If you do nothing he will report you to the police.)
=> END!



#######################################
# SCENE 1

~ wall
MC: It's a wall. They could probably hang a nice painting there.
=> END!

~ webcam
if state.webcam_off == false:
	It's a webcam. It's on.
	Turn it off?
	- Yes
		do turn_webcam_off()
	- No
else:
	It's a webcam. It's off.
=> END!

~ body
if inventory.body == false:
	It's a dead body. Gross.
	MC: J didn’t even do a good job, there are bruises and cuts everywhere…
	Pick up the body?
	- Yes
		do pickup_body()
		MC: Darn it J...since it's a rush job, I didn't bring any body bags with me.
		MC: I'll have to find a place to hide the body.
	- No
		Better leave it alone.
=> END!


~ blood_pool
if state.blood_cleaned == false:
	if mouse.current_state != mouse.State.MOP:
		MC: I'll need to find a way to clean up all this blood...
		MC: Smells like iron-scented kool aid.
		Drink the blood?
		- Yes
			do drink_blood()
			MC: Ahh... Delicious antioxidants.
		- No
	else:
		do mop_blood()

=> END!



~ gun
if inventory.gun == false:
	MC: Gun as a murder weapon?
	MC: That’s so last year... how lame. Might come in handy though.
	Pick up the gun?
	- Yes
		do pickup_gun()
	- No
	
=> END!





~ door
if mouse.current_state != mouse.State.KEY:
	It's the front door.
	Are you sure you want to leave?
	- Yes
		do exit_through_front_door()
	- No
		MC: Still more work to be done.
else:
	This door is already unlocked.

=> END!

~ trash
if mouse.current_state == mouse.State.BODY:
	MC: It's as good a place as any.
	Hide the body here?
	- Yes
		do hide_body("trash")
		MC: …yeeeah…that looks hidden.
	- No
		MC: I'll find another spot.
else:
	if state.hid_body_in_trash == true:
		MC: …yeeeah…that looks hidden.
	else:
		MC: Empty trash can. Very trash. Very can.
		
=> END!


~ computer
do toggle_computer()
if state.computer_on:
	It's a computer.
	MC: Why is this guy’s lockscreen a horseshoe crab?
=> END!


~ mouse_keyboard
do toggle_computer()
=> END!




~ chair
MC: It's a chair.
=> END!


~ storage_closet
if state.storage_closet_unlocked == true:
	if inventory.mop == false and state.blood_cleaned_mopped == false:
		MC: You open the closet, and there's a mop inside. Take the mop?
		- Yes
			MC: You took the mop.
			do pickup_mop()
		- No
			MC: You didn't take the mop.
	else:
		if mouse.current_state == mouse.State.BODY:
			MC: It's as good a place as any.
			Hide the body here?
			- Yes
				do hide_body("storage_closet")
				MC: Nice.
				# MC: No one's finding him in there.
			- No
				MC: I'll find another spot.
		else:
			if state.hid_body_in_storage_closet == true:
				if save_data.use_old_art == false:
					if closet_check_count == 0:
						MC: No one's finding him in there.
						do closet_check_count += 1
					else:
						do toggle_storage_closet_with_body()
				else:
					MC: No one's finding him in there.
			else:
				You check the closet, but there's nothing inside.
		
		
else:
	if mouse.current_state != mouse.State.KEY:
		do AudioManager.play_fx("locked_door_wiggle", -6.0)
		MC: Locked.
	else:
		do unlock_storage_closet()

=> END!

~ top_drawer
You rummage through the top drawer.
MC: Sonic, Halo, Minecraft... what a nerd.
=> END!

~ bottom_drawer
You rummage through the bottom drawer.
MC: Magic mushrooms? Hrmmmmm...
MC: Seems like a waste to just leave them here.
Eat the magic mushrooms?
- Yes
	MC: Alright, here goes nothing...
	do eat_mushrooms()
- No
	MC: Better not.
=> END!

~ mushroom
% MC: Oh crap... the walls are breathing... that's probably not good...
% MC: Oh crap... I think I can hear colors...
% MC: Oh crap... I think my eyeballs are melting...
% MC: Oh crap... I think my brain is melting...
=> END!

#######################################
# SCENE 2

~ lamp
do toggle_lamp()
=> END!



~ bed_top
It's a bed.
MC: Hmm, looks comfy. Maybe I could lie down for a moment.
Sit on the bed?
- Yes
	MC: Ahh... amazing... is this cashmere? So soft...
	do sleep_in_bed()
- No
	MC: What am I thinking... Gotta finish this job first.


=> END!

~ bed_bottom
if inventory.storage_closet_key == false and state.storage_closet_unlocked == false:
	Looking under the bed, you find a small key. Take it?
	- Yes
		do set_inventory_item("storage_closet_key", true)
	- No
		MC: Probably nothing important.
else:
	if mouse.current_state == mouse.State.BODY:
		MC: It's as good a place as any.
		Hide the body here?
		- Yes
			do hide_body("bed_bottom")
			MC: …yeeeah…that looks hidden.
		- No
			MC: I'll find another spot.
	else:
		if state.hid_body_under_bed == true:
			MC: …yeeeah…that looks hidden.
		else:
			MC: It’s kinda dusty down here…
		
=> END!


~ book_shelf
MC: Huh, wonder what this guy reads.
Pick a book to read?
- Yes
	Which one?
	- Guide to Inventory (For Dummies)
		"'Guide to Inventory (For Dummies)', by Big Dummy:"
		"...Always check under the bed for items."
		"...Click on the items in your inventory to equip them."
		"...Try clicking on other objects around the room."
		"...Different actions will occur depending on which item you have equipped."
		"...Click on the red X to unequip your items."
		MC: I feel dumber already.
		
	- How to Hide A Body
		"'How to Hide A Body', by M. Light Famalayan:"
		MC: Some pages are missing...
		"...Always check under the bed for items."
		"...Try mopping up the blood."
		"...Get rid of all the evidence before time runs out."
		"...When you are ready, click the front door to leave."
		MC: Who wrote this garbage?
	
	- The Basics of Horseshoe Crab Racing
		"'The Basics of Horseshoe Crab Racing', by Alice Jones:"
		“...Horseshoe crabs are crustaceans.”
		“...They are not that fast.”
		“...They can be stealthy.”
		MC: The heck?
	
	- Theatre 101 Syllabus
		“'Theatre 101 Syllabus', by T. A. Cher:”
		"...This semester will be featuring [color=yellow]{{correct_play}}[/color] by [color=yellow]{{correct_author}}[/color]."
		"Be sure to attend all rehearsals for a passing grade."
		MC: Hope this guy wasn't the lead performer.
- No

=> END!

~ night_stand
if mouse.current_state == mouse.State.GUN:
	Hide the gun in the nightstand drawer?
	- Yes
		do hide_gun("nightstand")
		MC: ...
		MC: I guess that'll have to do.
	- No
		MC: I'll find another way.
else:
	On the nightstand, you find a sticky note.
	Read it?
	- Yes
		Sticky note, reads:
		To do:
		Brush teeth
		Audition for {{club_name}}
		Brush teeth
		Find another race candidate
		Brush teeth
		Go to the ring
		Brush teeth
		MC: Why does this guy like brushing his teeth so much?
	- No

=> END!


#######################################
# SCENE 3

#~ bed_top
#MC: It's a bed.
#=> END!

~ sink
if mouse.current_state == mouse.State.BODY:
	MC: It's as good a place as any.
	Hide the body here?
	- Yes
		do hide_body("sink")
		MC: ...
		MC: ...
		% MC: Holy cow... The garbage disposal on this thing is no joke.
		% MC: Holy guacamole... The garbage disposal on this thing is no joke.
		% MC: Holy ham fat... The garbage disposal on this thing is no joke.
		% MC: Holy macaroni... The garbage disposal on this thing is no joke.
		% MC: Great Scott! The garbage disposal on this thing is no joke.
		% MC: Zounds! The garbage disposal on this thing is no joke.
		MC: Really lives up to the hype.
	- No
		MC: I'll find another spot.
elif mouse.current_state == mouse.State.GUN:
	Put the gun in the garbage disposal?
	- Yes
		do hide_gun("sink")
		MC: ...
		MC: ...
		% MC: Holy cow... The garbage disposal on this thing is no joke.
		% MC: Holy guacamole... The garbage disposal on this thing is no joke.
		% MC: Holy ham fat... The garbage disposal on this thing is no joke.
		% MC: Holy macaroni... The garbage disposal on this thing is no joke.
		% MC: Great Scott! The garbage disposal on this thing is no joke.
		% MC: Zounds! The garbage disposal on this thing is no joke.
		MC: Really lives up to the hype.
	- No
		MC: I'll find another way.
else:
	It's a sink.
	- Wash hands
		if state.washed_hands == false:
			Are you sure? You'll have to take your gloves off.
			- Yes
				Your fingerprints are gonna be everywhere.
				- I don't care
					Ooookayyy. Whatever you want buddy.
					do wash_hands()
					*splash splash*
					*rub rub*
					*rinse rinse*
					There. You happy now?
					- Yes
						Good for you.
				- Nevermind then
			- Nevermind then
		else:
			do wash_hands()
			if wash_count == 2:
				Sure. Why not?
			elif wash_count == 3:
				These hands must be filthy.
			elif wash_count == 4:
				Like, really filthy.
			elif wash_count == 5:
				Incredibly, incredibly filthy.
			elif wash_count == 6:
				[b]Unbelievably[/b] filthy.
			elif wash_count == 7:
				So filthy, that nothing could possibly clean them.
				Not even a magnificent and powerful industrial sink like this one.
			elif wash_count == 8:
				Even if you scrubbed them down to the very bone, these hands would still be, in fact, filthy.
			elif wash_count == 9:
				No matter how many times you do it, it won't make a difference.
			elif wash_count == 10:
				do unlock_achievement("ending_clean_hands")
				do wash_count = 1
				In the end, it doesn't really matter what it is.
				Nothing could ever truly wipe these hands of yours clean.
				Nothing...
			
	- Read manufacturer tag
		"LARGE INDUSTRIAL MEGA SINK WITH SUPER MEGA INDUSTRIAL DIAMOND CLAW GARBAGE DISPOSAL"
		MC: Why is this even here? Dorm layouts make no sense... And where's the toilet?

=> END!


~ sink_shelf
It's a shelf.
MC: Why does this guy have so many toothbrushes?
=> END!


~ mirror
if state.blood_cleaned_drank == true:
	do unlock_achievement("ending_vampire")
	MC: Weird, I can't see anything. This mirror must be broken or something.
else:
	It's a mirror. You take a moment to admire yourself in it.
	MC: Who's that handsome devil?
=> END!


~ floor_mat
MC: It's a floor mat.
=> END!


~ toilet
MC: Don't tell me... Is that the toilet?
=> END!


#######################################
# SCENE 4

#~ bed_top
#MC: It's a bed.
#=> END!

#~ sink
#MC: It's a sink.
#=> END!

~ window
if state.window_event == true:
	if mouse.current_state == mouse.State.GUN:
		do intimidation()
		???: AHHH! Please don't shoot mister!
		MC: You gonna keep your mouth shut, punk?
		???: A-hh-..I..--I mean, about what, sir?
		MC: I mean, you didn't see nothing suspicious here, am I right?
		???: Oh--oh, yes, yes of course sir.
		MC: And we ain't gonna be calling no men in blue uniforms, are we now?
		???: Uhh... no sir, no. I swear.
		MC: You swear? *puts gun to forehead*
		???: AHHHH!! ...
		???: I.... I swear... Please let me go... *sobs*
		MC: Alright. Scram.
		???: Th-th...Thanks mister... You won't regret this!
		do stop_window_event(true)
		
	else:
		MC: Hello there.
		???: Hey man. What's going on here?
		MC: Uhh...
		- Ignore him
			if state.hid_body == false:
				???: Dude... I think that guy's dead. I'm calling the cops.
			else:
				???: Hmmm... something's not right here. I'm calling the kappas.
			do stop_window_event(false)
		- Attempt to use jedi mind tricks
			???: ...
			???: I dunno what's going on here, but that was super weird. I'm calling the cops.
			do stop_window_event(false)
		- Make up an excuse
			What's your excuse?
			- I killed someone, obviously.
				???: ...
				???: Riiiiight. Yeah I'm calling the cops.
				do stop_window_event(false)
			- We're filming a commercial.
				???: ...
				???: Hmph, I dunno. Seems suspicious. I'm calling the cops.
				do stop_window_event(false)
			- It's for a school play.
				???: Oh yeah? Which one?
				- Death of a Salesman.
					do response = "Death of a Salesman"
				- To Kill a Mockingbird.
					do response = "To Kill a Mockingbird"
				- Les Miserables.
					do response = "Les Miserables"
				if response == correct_play:
					???: Right on. And who wrote that one again?
					- Arthur Miller.
						do response = "Arthur Miller"
					- Harper Lee.
						do response = "Harper Lee"
					- Victor Hugo.
						do response = "Victor Hugo"
					if response == correct_author:
						do unlock_achievement("playwright")
						???: Hmm. You seem to really know your stuff. I guess I can trust you.
						???: Be more careful next time. Somebody might think there was a murder or something.
						MC: Roger that. Don't worry, no murders happening here!
						???: Hahahaha... Yeah. I mean that body over there just looks [b]super[/b] realistic. Crazy.
						MC: Yeah, our makeup artists, they're crazy good.
						???: Hmm... Alright then. I won't bother you anymore. Have a good one.
						MC: See ya later. (I hope not...)
						???: What was that?
						MC: Nothing. Bye!
						???: Bye...
						do stop_window_event(true)
						
					else:
						???: Hmph, I dunno. Seems suspicious. I'm calling the cops.
						do stop_window_event(false)
				else:
					???: Hmph, I dunno. Seems suspicious. I'm calling the cops.
					do stop_window_event(false)

else:
	if state.curtains_closed == false:
		MC: It's a window. Might want to close the curtains.
=> END!


~ curtain
if state.window_event == false:
	if state.curtains_closed == false:
		MC: It's the curtains. Pull them shut?
		- Yes
			do close_curtains()
			MC: You closed the curtains.
			
		- No
			MC: You didn't close the curtains.
	else:
		MC: It's the curtains. Open them?
		- Yes
			do open_curtains()
			MC: You opened the curtains.
			
		- No
			MC: You didn't open the curtains.
=> END!